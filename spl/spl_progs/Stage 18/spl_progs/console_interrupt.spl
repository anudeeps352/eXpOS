//Storing SP value in UPTR field of Process Table
	[PROCESS_TABLE + ([SYSTEM_STATUS_TABLE + 1] *16) + 13]=SP;


//Setting SP to UArea Page Number * 512 - 1;
	SP=[PROCESS_TABLE + ([SYSTEM_STATUS_TABLE + 1] *16) + 11] * 512 - 1;

//backing up user context
	backup;

//Obtaining PID of process that requires terminal
	alias reqPID R2;
	reqPID = [TERMINAL_STATUS_TABLE + 1];

//Obtaining corresponding process table entry
	alias process_table_entry R1;
	process_table_entry = PROCESS_TABLE + reqPID * 16;

//Copying value in P0 to input buffer in process table
	[process_table_entry + 8]=P0;


//Releasing the terminal
	multipush(R1,R2);
	alias funcNum R1;
	funcNum=9;             //reqPID is already stored in R2.So no need to load it into the register again
	call MOD_0;
	multipop(R1,R2);
	

//Restoring user context
restore;

//Switching back to user mode
SP=[PROCESS_TABLE + ([SYSTEM_STATUS_TABLE +1] *16) + 13];         //USER SP 
ireturn;
	









